///////// Start symbol ///////// done

Program -> StatementList $

///////// Statement ///////// underway

StatementList -> Statement StatementList
StatementList -> ə

Statement -> DeclAssignOrFuncCall 
Statement -> FuncDeclOrDef 
Statement -> Return
Statement -> Block
Statement -> ConditionalStatement
Statement -> Loop

Statement -> 'break' ';'
Statement -> 'continue' ';' // Add a way to specify which loop to continue

///////// DeclAssignOrFuncCall ///////// done

DeclAssignOrFuncCall -> identifier OptionalType OptionalAssign ';'
DeclAssignOrFuncCall -> identifier ArraySubscript AssignmentStub ';'
DeclAssignOrFuncCall -> identifier FuncCall ';'

OptionalType -> Type
OptionalType -> ə

OptionalAssign -> AssignmentStub
OptionalAssign -> ə

AssignmentStub -> '=' Expr

Block -> '{' StatementList '}'

///////// Func call ///////// done

FuncCall -> '(' ArgumentList ')'

ArgumentList -> NonTupleExpr ',' ArgumentList
ArgumentList -> NonTupleExpr
ArgumentList -> ə

///////// Func Declarations and Definitions ///////// done

FuncDeclOrDef -> 'func' identifier FuncDeclParam FuncDeclType ';'
FuncDeclOrDef -> 'func' identifier FuncDeclParam FuncDeclType Block

FuncDeclParam -> '(' ParameterList ')'
FuncDeclParam -> ə

FuncDeclType -> '->' Type
FuncDeclType -> ə

ParameterList -> Parameter ',' ParameterList
ParameterList -> Parameter
ParameterList -> ə

Parameter -> identifier Type
Parameter -> Type

Return -> 'return' Expr ';'

///////// EXPRESSIONS ///////// done

Expr -> BinaryOpAddPrec Tuple
Tuple -> ',' BinaryOpAddPrec Tuple
Tuple -> ə

NonTupleExpr -> BinaryOpAddPrec

BinaryOpAddPrec -> BinaryOpMulPrec BinaryOpAddPrecR 

BinaryOpAddPrecR -> '+' BinaryOpMulPrec BinaryOpAddPrecR
BinaryOpAddPrecR -> '-' BinaryOpMulPrec BinaryOpAddPrecR
BinaryOpAddPrecR -> ə

BinaryOpMulPrec -> UnaryOp BinaryOpMulPrecR 

BinaryOpMulPrecR -> '*' UnaryOp BinaryOpMulPrecR
BinaryOpMulPrecR -> '/' UnaryOp BinaryOpMulPrecR
BinaryOpMulPrecR -> ə

UnaryOp -> '-' UnaryOp
UnaryOp -> '@' UnaryOp // Address of
UnaryOp -> '^' UnaryOp // Deref
UnaryOp -> '!' UnaryOp
UnaryOp -> ExprTerm

ExprTerm -> '(' Expr ')'
ExprTerm -> identifier FuncCall
ExprTerm -> identifier ArraySubscript
ExprTerm -> identifier AssignmentStub
ExprTerm -> identifier
ExprTerm -> number
ExprTerm -> '"' string '"'

ArraySubscript -> '[' Expr ']'

///////// Type ///////// underway

Type -> TypeModifiers

TypeModifiers -> BaseType TypeModifiersR
TypeModifiersR -> '^' TypeModifiersR
TypeModifiersR -> '[' Expr ']' TypeModifiersR
TypeModifiersR -> '[]' TypeModifiersR
TypeModifiersR -> ə

BaseType -> identifier
BaseType -> type

type -> void
type -> u?short
type -> u?int
type -> u?long

type -> u?char
type -> bool
type -> string

type -> float
type -> double

///////// Conditional Statements /////////

ConditionalStatement -> 'if' Condition Block OptionalElseblock

Condition -> '(' Expr ')'

OptionalElseblock -> 'else' Block
OptionalElseblock -> ə

///////// Loops /////////

Loop -> 'for' RangeSpec Block

RangeSpec -> '(' Statement ';' Statement ';' Statement ')' // C style for
RangeSpec -> '(' identifier 'in' Expr ')' // Foreach
RangeSpec -> ə // Infinite loop