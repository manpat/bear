///////// Start symbol ///////// done

Program -> StatementList $

///////// Statement ///////// done

StatementList -> Statement StatementList
StatementList -> ə

Statement -> DeclAssignOrFuncCall 
Statement -> FuncDeclOrDef 
Statement -> Return
Statement -> Block

///////// DeclAssignOrFuncCall ///////// done

DeclAssignOrFuncCall -> identifier OptionalType OptionalAssign ';'
DeclAssignOrFuncCall -> identifier FuncCall ';'

OptionalType -> Type
OptionalType -> ə

OptionalAssign -> AssignmentStub
OptionalAssign -> ə

AssignmentStub -> '=' Expr

Block -> '{' StatementList '}'

///////// Func call /////////

FuncCall -> '(' ArgumentList ')'

ArgumentList -> NonTupleExpr ',' ArgumentList
ArgumentList -> NonTupleExpr
ArgumentList -> ə

///////// Func Declarations and Definitions /////////

FuncDeclOrDef -> 'func' identifier FuncDecl
FuncDeclOrDef -> 'func' identifier FuncDecl Block

FuncDecl -> FuncDeclParam FuncDeclType

FuncDeclParam -> '(' ParameterList ')'
FuncDeclParam -> ə

FuncDeclType -> '->' Type
FuncDeclType -> ə

ParameterList -> Parameter ',' ParameterList
ParameterList -> Parameter
ParameterList -> ə

Parameter -> identifier Type
Parameter -> Type

Return -> 'return' Expr ';'

///////// EXPRESSIONS ///////// done

Expr -> BinaryOpAddPrec Tuple
Tuple -> ',' BinaryOpAddPrec Tuple
Tuple -> ə

NonTupleExpr -> BinaryOpAddPrec

BinaryOpAddPrec -> BinaryOpMulPrec BinaryOpAddPrecR 

BinaryOpAddPrecR -> '+' BinaryOpMulPrec BinaryOpAddPrecR
BinaryOpAddPrecR -> '-' BinaryOpMulPrec BinaryOpAddPrecR
BinaryOpAddPrecR -> ə

BinaryOpMulPrec -> UnaryOp BinaryOpMulPrecR 

BinaryOpMulPrecR -> '*' UnaryOp BinaryOpMulPrecR
BinaryOpMulPrecR -> '/' UnaryOp BinaryOpMulPrecR
BinaryOpMulPrecR -> ə

UnaryOp -> '-' UnaryOp
UnaryOp -> '@' UnaryOp // Address of
UnaryOp -> '^' UnaryOp // Deref
UnaryOp -> '!' UnaryOp
UnaryOp -> ExprTerm

ExprTerm -> '(' Expr ')'
ExprTerm -> identifier FuncCall
ExprTerm -> identifier
ExprTerm -> identifier AssignmentStub
ExprTerm -> number
ExprTerm -> '"' string '"'

///////// Type ///////// underway

Type -> TypeModifiers

TypeModifiers -> BaseType TypeModifiersR
TypeModifiersR -> '^' TypeModifiersR
TypeModifiersR -> '[' Expr ']' TypeModifiersR
TypeModifiersR -> '[]' TypeModifiersR
TypeModifiersR -> ə

BaseType -> identifier
BaseType -> type

type -> void
type -> u?short
type -> u?int
type -> u?long

type -> u?char
type -> string

type -> float
type -> double